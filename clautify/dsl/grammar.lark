// Spotify DSL Grammar (v4)
//
// Uniform verb-kind-target pattern.
// Kind is always singular: track, album, artist, playlist.
// Targets: quoted strings (auto-resolved via search) or bare 22-char IDs.

start: action state_modifier*
     | query query_modifier*
     | state_modifier+

// === Actions ===

?action: "play" kind target context?                    -> play
       | "pause"                                        -> pause
       | "resume"                                       -> resume
       | "skip" SIGNED_NUMBER?                          -> skip
       | "seek" NUMBER                                  -> seek
       | "queue" kind target+                           -> queue
       | "library" LIB_MUT kind target+ context?        -> library_mut
       | "library" "create" "playlist" ESCAPED_STRING   -> library_create
       | "library" "delete" "playlist" target           -> library_delete

// === Queries ===

?query: "search" kind ESCAPED_STRING+                   -> search
      | "info" kind target                              -> info
      | "recommend" kind NUMBER? context                -> recommend
      | "status"                                        -> status
      | "library" "list" kind                           -> library_list

// === Context (reusable: "in" kind target) ===

context: "in" kind target

// === State Modifiers (standalone or chained onto actions) ===

?state_modifier: "volume" VOL_DELTA                     -> volume_rel
               | "volume" NUMBER                        -> volume
               | "mode" MODE                            -> mode
               | "device" ESCAPED_STRING                 -> device

// === Query Modifiers ===

?query_modifier: "limit" NUMBER                         -> limit
               | "offset" NUMBER                        -> offset

// === Shared ===

?target: BARE_ID | ESCAPED_STRING

kind: KIND_KW
KIND_KW: "track"i | "album"i | "artist"i | "playlist"i

LIB_MUT: "add"i | "remove"i
MODE: "normal"i | "shuffle"i | "repeat"i
BARE_ID: /[a-zA-Z0-9]{22}/
VOL_DELTA: /[+-]\d+/

%import common.ESCAPED_STRING
%import common.SIGNED_NUMBER
%import common.NUMBER
%import common.WS
%ignore WS
